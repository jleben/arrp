module signal;
import array;
import math;

... FIXME: The following definition fails type checking if a has size [~,3]:
... delay(v,a) = v ++ a;

delay(v,x) = y where {
    y[0] = v;
    y[t] = x[t-1];
};

win(size,hop,x) = [t] -> [k:size] -> x[t*hop + k];

phase(freq, init) = p where
{
    p[0] = init;
    p[t] = wrap(p[t-1] + freq[t-1]) where
        wrap(x) = if x < 1 then x else x - 1;
};


sine(freq, init_ph) =
  let ph = phase(freq, init_ph) in
    sin(ph * 2 * math.pi);

triangle(freq, delay) =
  let ph = phase(freq, delay) in
    1 - abs(ph*2 - 1);

biquad(a,b,x) = [n] -> y[n+2] where
{
    y[0] = 0;
    y[1] = 0;
    y[n] = x[n] * b[0] + x[n-1] * b[1] + x[n-2] * b[2]
                       - y[n-1] * a[0] - y[n-2] * a[1];
};
